{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "751d4ebe",
   "metadata": {},
   "source": [
    "# Comparing degenerations of spinor varieties\n",
    "\n",
    "*Authors: George Balla, Daniel Corey, Igor Makhlin, and Victoria Schleis*\n",
    "\n",
    "In this notebook accompanying our paper \"Regular subdivisions, bounds on initial ideals, and categorical limits\", we compute degenerations of spinor varieties. For spinor varieties of type $D_5$ and $D_6$, we compare $\\mathrm{in}_w(I)$ and $I_w$ for tropical vectors $w$, where $I_w$ is the lower bound as defined in our paper. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "03b7ab2c-249b-4b27-86a7-ca2618a0a477",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Oscar\n",
    "using Combinatorics\n",
    "pm = Polymake\n",
    "\n",
    "include(\"InitialIdealsRegularSubdivisions.jl\")\n",
    "include(\"gfan_functions.jl\");"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f7d63937",
   "metadata": {},
   "source": [
    "\n",
    "\n",
    "## Spinor varieties\n",
    "\n",
    "\n",
    "The space of isotropic subspaces of a $2n$-dimensional vector space equipped with a symmetric, nondegenerate form is an algebraic variety which consists of two irreducible components that are projectively equivalent to each other. The _spinor variety_ $\\mathbb{S}_{n}$ is one of these irreducible components. It is a projective variety via the Wick embedding $J_n\\subset S$ where $S$ is a polynomial ring with a generator for each subset of $[1,n]$ that has an even number of elements as $n$ modulo $2$. In [[Corey 2024](https://doi.org/10.1007/s40879-023-00715-z)], it is shown that $(J_n^{\\circ})_w = \\mathrm{in}_{w}(J_n)^{\\circ}$ for $n\\leq 5$ and $w\\in \\mathrm{Trop}(J_n)$. Here, we study the (stronger) equality $(J_n)_w = \\mathrm{in}_{w}(J_n)$ for $n=5,6$ and $w\\in \\mathrm{Trop}(J_n)$.\n",
    "\n",
    "---\n",
    "\n",
    "**Function:** `spinor_variety_D(n)`\n",
    "\n",
    "*Description*: For an integer `n` computes the ideal generating the spinor variety of type $D_n$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "770078cc-54b8-42d5-bd4a-65845bac95ab",
   "metadata": {},
   "outputs": [],
   "source": [
    "l(i, I) = length([j for j in I if j < i])\n",
    "\n",
    "function spinor_variety_D(n)\n",
    "    Le = vcat([reverse.(sort(reverse.(subsets(n, k)))) for k in 0:2:n]...)\n",
    "    Lo = vcat([subsets(n, k) for k in 1:2:n]...)\n",
    "    RDe, xDe = graded_polynomial_ring(QQ, :xDe => Le)\n",
    "    xDe = Dict(k => v for (k, v) in zip(Le, xDe))\n",
    "\n",
    "    relsDe = []\n",
    "    for (I, J) in subsets(Lo, 2)\n",
    "        SD = symdiff(I, J)\n",
    "        if length(SD) >= 4\n",
    "            push!(relsDe, sum([(-1)^(l(i, I) + l(i, J))*xDe[sort(symdiff(I, i))]*xDe[sort(symdiff(J, i))] for i in SD]))\n",
    "        end\n",
    "    end\n",
    "    return ideal(relsDe)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "af0d9bd9",
   "metadata": {},
   "source": [
    "*Example*: We compute ideals of the spinor varieties of type $D_5$ and $D_6$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "072efb11",
   "metadata": {},
   "outputs": [],
   "source": [
    "I = spinor_variety_D(5)\n",
    "\n",
    "J = spinor_variety_D(6);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8bba13f6",
   "metadata": {},
   "source": [
    "## The case $n = 5 $\n",
    "\n",
    "In this section we focus on the spinor variety associated to $D_5$, whose ideal `I` we computed in the previous section. \n",
    "\n",
    "We briefly recall the definitions of $I_w$ and $\\Omega(I)$. Let $\\Delta = \\mathcal{A}(I)$ be the point configuration of $I$. A vector $w\\in \\mathbb{R}^{n}$ induces a regular subdivision $\\mathrm{subd}_{w}(\\Delta)$ of $\\Delta$. We can form the ideals \n",
    "\n",
    "$$\\{\\tilde{I}_{B} \\, : \\, B \\text{ is a maximal cell of } \\mathrm{subd}_{w}(\\Delta)\\}$$\n",
    "\n",
    "and the ideal they generate is\n",
    "\n",
    "$$I_{w} = \\sum_{B \\text{ maximal}} \\tilde{I}_{B}. $$\n",
    "\n",
    "---\n",
    "\n",
    "To show that $\\mathrm{in}_w(I) = I_w$ for all tropical vectors $w$, we compute $\\mathrm{Trop}(I)$ using `tropical_variety`. For each maximal cone in  $\\mathrm{Trop}(I)$, we obtain a weight $w$ in the relative interior. We first verify that the maximal tropical is contained in the secondary cone of $\\Delta$ containing $w$, to make sure that checking a single interior point is sufficient. We then compare the initial ideal $\\mathrm{in}_w(I)$ (computed via `initial`) to the ideal $I_w$ we compute using `ideal_w` as documented in `Computing_I_w_and_Omega.ipynb`. We count the number of times equality is achieved and compare this value to the number of maximal cones of $\\mathrm{Trop}(I)$, obtaining equality.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0b92a2d9",
   "metadata": {},
   "outputs": [],
   "source": [
    "R = base_ring(I)\n",
    "\n",
    "RR, y = polynomial_ring(QQ, length(gens(R)), :y)\n",
    "g = hom(R, RR, gens(RR))\n",
    "II = g(I)\n",
    "TV = tropical_variety(II, tropical_semiring_map(QQ, max))[1]\n",
    "println(length(maximal_polyhedra(TV)))\n",
    "\n",
    "D = point_configuration(I)\n",
    "\n",
    "c = 0\n",
    "for C in maximal_polyhedra(TV)\n",
    "    rml = rays_modulo_lineality(C)[1]\n",
    "    \n",
    "    w = sum(rml)\n",
    "    w = lcm(denominator.(w))*w\n",
    "    \n",
    "    SC = secondary_cone(subdivision_of_points(D, w))\n",
    "    if !prod(r in SC for r in rml)\n",
    "        println(\"!!!\")\n",
    "    end\n",
    "    \n",
    "    inI = initial(I, tropical_semiring_map(QQ), w)\n",
    "    \n",
    "    c += ideal_w(I, w) == inI\n",
    "end\n",
    "println(c)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "18b5915c",
   "metadata": {},
   "source": [
    "## The case $n = 6$ \n",
    "\n",
    "We now turn to the spinor variety associated to $D_6$, given by the ideal `J` computed above. On the whole, we proceed very similarly to the case $n=5$, however, as the computation of the tropical variety takes a while, we load it from the result of a pre-computation using the function `parse_tropical_file` which we provide in the supplementary file `gfan_functions.jl`. \n",
    "\n",
    "For the sake of running time, this computation only performs the comparison for the last 1000 maximal cones in the tropical variety. It finds zero cases of equality, in contrast to the $n=5$ case."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bb54ef00",
   "metadata": {},
   "outputs": [],
   "source": [
    "rs, M = parse_tropical_file(\"precomputed_data/n6trop.txt\", true)\n",
    "\n",
    "D = point_configuration(J)\n",
    "\n",
    "c = 0\n",
    "for i in nrows(M) - 1000 : nrows(M)\n",
    "    rml = [-r for (j, r) in enumerate(rs) if M[i, j]]\n",
    "    w = sum(rml)\n",
    "    \n",
    "    SC = secondary_cone(subdivision_of_points(D, w))\n",
    "    if !prod(r in SC for r in rml)\n",
    "        println(\"!!!\")\n",
    "    end    \n",
    "    \n",
    "    inJ = initial(J, tropical_semiring_map(QQ), w)\n",
    "    Jw = ideal_w(J, w)\n",
    "    \n",
    "    c += Jw == inJ\n",
    "end\n",
    "c"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
