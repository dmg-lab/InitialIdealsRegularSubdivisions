{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "751d4ebe",
   "metadata": {},
   "source": [
    "# Comparing degenerations of spinor varieties\n",
    "\n",
    "*Authors: George Balla, Daniel Corey, Igor Makhlin, and Victoria Schleis*\n",
    "\n",
    "In this notebook accompanying our paper \"Regular subdivisions, bounds on initial ideals, and categorical limits\", we compute degenerations of spinor varieties. We show that for spinor varieties of Type $D_5$ and $D_6$, we have that $\\mathrm{in}_w(I) = I_w$, where $I_w$ is the lower bound as defined in our paper. \n",
    "\n",
    "We refer to `Computing_I_w_and_Omega.ipynb` for a thorough explanation and documentation of the lower bound ideal $I_w$ and the computation of the fan $\\Omega(I) = \\{w \\in \\mathbb{R}^n | \\mathrm{in}_w(I) = I_w\\}$, whereas this notebook only covers the extended example computation for spinor varieties. \n",
    "    "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "03b7ab2c-249b-4b27-86a7-ca2618a0a477",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Oscar;\n",
    "using Combinatorics;\n",
    "pm = Polymake;\n",
    "\n",
    "include(\"InitialIdealsRegularSubdivisions.jl\")\n",
    "include(\"gfan_functions.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f7d63937",
   "metadata": {},
   "source": [
    "\n",
    "\n",
    "## Spinor varieties\n",
    "\n",
    "\n",
    "The space of totally isotropic subspaces of a $2n$-dimensional vector space equipped with a symmetric, nondegenerate form of Witt index $n$ is an algebraic variety which consists of two irreducible components that are projectively equivalent to each other. The _spinor variety_ $\\mathbb{S}_{n}$ is one of these irreducible components. It is a projective variety via the Wick embedding $J_n\\subset S$ where $S$ is a polynomial ring with a generator for each subset of $[1,n]$ that has the same number of elements as $n$ modulo $2$. In [Cor], it is shown that $(J_n^{\\circ})_w = \\mathrm{in}_{w}(J_n)^{\\circ}$ for $n\\leq 5$ and $w\\in \\mathrm{Trop}(J_n)$. Here, we demonstrate the equality $(J_n)_w = \\mathrm{in}_{w}(J_n)$ for $n\\leq 5$ and $w\\in \\mathrm{Trop}(J_n)$.\n",
    "\n",
    "---\n",
    "\n",
    "**Function:** `spinor_variety_D(n)`\n",
    "\n",
    "*Description*: For an integer `n` computes the ideal generating the Spinor variety of type $D_n$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "770078cc-54b8-42d5-bd4a-65845bac95ab",
   "metadata": {},
   "outputs": [],
   "source": [
    "function spinor_variety_D(n)\n",
    "n = n-1\n",
    "l(i, I) = length([j for j in I if j < i])\n",
    "Le = vcat([reverse.(sort(reverse.(subsets(n+1, k)))) for k in 0:2:n+1]...)\n",
    "Lo = vcat([subsets(n+1, k) for k in 1:2:n+1]...)\n",
    "RDe, xDe = graded_polynomial_ring(QQ, :xDe => Le)\n",
    "xDe = Dict(k => v for (k, v) in zip(Le, xDe))\n",
    "\n",
    "relsDe = []\n",
    "for (I, J) in subsets(Lo, 2)\n",
    "    SD = symdiff(I, J)\n",
    "    if length(SD) >= 4\n",
    "        push!(relsDe, sum([(-1)^(l(i, I) + l(i, J))*xDe[sort(symdiff(I, i))]*xDe[sort(symdiff(J, i))] for i in SD]))\n",
    "    end\n",
    "end\n",
    "return ideal(relsDe)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "af0d9bd9",
   "metadata": {},
   "source": [
    "*Example*: We compute ideals of the spinor varieties of type $D_5$ and $D_6$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "072efb11",
   "metadata": {},
   "outputs": [],
   "source": [
    "I = spinor_variety_D(5)\n",
    "\n",
    "J = spinor_variety_D(6);"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8bba13f6",
   "metadata": {},
   "source": [
    "## The case $n = 5 $\n",
    "\n",
    "In this section we focus on the Spinor variety associated to $D_5$, whose ideal `I` we computed in the previous section. \n",
    "\n",
    "We briefly recall the definitions of $I_w$ and $\\Omega(I)$. Let $\\Delta = \\mathcal{A}(I)$ be the point configuration of $I$. A vector $w\\in \\mathbb{R}^{n}$ induces a regular subdivision $\\mathrm{subd}_{w}(\\Delta)$ of $\\Delta$. We can form the ideals \n",
    "\n",
    "$$\\{\\tilde{I}_{B} \\, : \\, B \\text{ is a maximal cell of } \\mathrm{subd}_{w}(\\Delta)\\}$$\n",
    "\n",
    "and the ideal they generate is\n",
    "\n",
    "$$I_{w} = \\sum_{B \\text{ maximal}} \\tilde{I}_{B}. $$\n",
    "\n",
    "For an ideal $I$ with point configuration $\\Delta$, we define $\\Omega(I)$ by\n",
    "\n",
    "$$ \\Omega(I) = \\{w \\in \\mathbb{R}^{n} \\, : \\, \\mathrm{in}_{w} I = I_{w} \\}. $$\n",
    "\n",
    "This set is the support of a subfan of the secondary fan $\\mathrm{Sec}(\\Delta)$.\n",
    "\n",
    "---\n",
    "\n",
    "To show that $\\mathrm{in}_w(I) = I_w$ for all weight vectors $w$, we compute $\\mathrm{Trop}(I)$ using `tropical_variety`. For each maximal cone in  $\\mathrm{Trop}(I)$, we obtain a weight $w$ in the relative interior, and compare the initial degeneration $\\mathrm{in}_w(I)$ (computed via `initial`) to the ideal $I_w$ we compute using `ideal_w` as documented in `Computing_I_w_and_Omega.ipynb`. In the process, we count whenever equality is achieved. We then compare this number to the number of maximal cells of $\\mathrm{Trop}(I)$, and obtain equality.\n",
    " "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0b92a2d9",
   "metadata": {},
   "outputs": [],
   "source": [
    "RR, y = polynomial_ring(QQ, length(gens(base_ring(I)), :y);\n",
    "g = hom(base_ring(I), RR, gens(RR));\n",
    "II = g(I);\n",
    "TV = tropical_variety(II)[1];\n",
    "println(length(maximal_polyhedra(TV)))\n",
    "\n",
    "c = 0;\n",
    "inIs = Set([]);\n",
    "for C in maximal_polyhedra(TV)\n",
    "    w = sum(rays_modulo_lineality(C)[1])\n",
    "    w = lcm(denominator.(w))*w\n",
    "    \n",
    "    inI = initial(I, tropical_semiring_map(QQ, max), w)\n",
    "    push!(inIs, inI)\n",
    "    \n",
    "    c += ideal_w(Ifour, -w) == inI\n",
    "end\n",
    "println(c, \"\\n\", length(inIs))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "18b5915c",
   "metadata": {},
   "source": [
    "## The case $n = 6$ \n",
    "\n",
    "We now turn to the Spinor variety associated to $D_6$, given by the ideal `J` computed above. Similar to the previous section, we wish to show that $\\mathrm{in}_w(J) = J_w$ for all $w \\in \\mathrm{Trop}(J)$. On the whole, we proceed very similarly to the case $n=5$, however, as the computation of the tropical variety takes a while, we load it in pre-computed using the function `parse_tropical_file` which we provide in the supplementary file `gfan_functions.jl`. \n",
    "\n",
    "Afterwards, we proceed analogously to the above:  For each maximal cone in  $\\mathrm{Trop}(J)$, we obtain a weight $w$ in the relative interior, and compare the initial degeneration $\\mathrm{in}_w(J)$ (computed via `initial`) to the ideal $J_w$ we compute using `ideal_w` as documented in `Computing_I_w_and_Omega.ipynb`. In the process, we count whenever equality is achieved. We then compare this number to the number of maximal cells of $\\mathrm{Trop}(J)$, and obtain equality. Again, for the sake of running time, this notebook only performs the comparison for the last 1000 maximal cones in the tropical variety."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bb54ef00",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "rays, M = parse_tropical_file(\"precomputed_data/n6trop.txt\", true);\n",
    "\n",
    "c = 0\n",
    "inJs = Set([])\n",
    "for i in nrows(M) - 1000 : nrows(M)\n",
    "    R = [r for (j, r) in enumerate(rays) if M[i, j]]\n",
    "    w = sum(R)\n",
    "    inJ = initial(J, tropical_semiring_map(QQ, max), w)\n",
    "    \n",
    "    Jw = ideal_w(J, -w)\n",
    "    c += Jw == inJ\n",
    "    \n",
    "    push!(inJs, inJ)\n",
    "end\n",
    "c\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
